# Title:
#   Ontology Must Have an English Title or Label
# Constraint Description:
#   The owl:Ontology node must have at least one dcterms:title or rdfs:label with @en.
# Severity:
#   Error
#
# A valid ontology should return ZERO rows.

PREFIX rdf:    <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl:    <http://www.w3.org/2002/07/owl#>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms:<http://purl.org/dc/terms/>

SELECT ?ont
WHERE {
  ?ont rdf:type owl:Ontology .

  # "Bad" if there is no English title
  FILTER NOT EXISTS {
    ?ont dcterms:title ?tTitle .
    FILTER(LANG(?tTitle) = "en")
  }

  # ...and also no English label
  FILTER NOT EXISTS {
    ?ont rdfs:label ?tLabel .
    FILTER(LANG(?tLabel) = "en")
  }
}
